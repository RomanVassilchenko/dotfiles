{ host, ... }:
let
  inherit (import ../../../hosts/${host}/variables.nix) gitUsername;
in
{
  programs.gh.enable = true;

  # GitHub-specific git configuration
  home.file.".config/git/github".text = ''
    [user]
      name = Roman Vassilchenko
      email = 36629811+RomanVassilchenko@users.noreply.github.com
  '';

  programs.git = {
    enable = true;

    # Include secrets file (generated by systemd service)
    includes = [
      { path = "~/.config/git/secrets"; }
      # GitHub-specific configuration
      {
        condition = "hasconfig:remote.*.url:git@github.com:*/**";
        path = "~/.config/git/github";
      }
      {
        condition = "hasconfig:remote.*.url:https://github.com/**";
        path = "~/.config/git/github";
      }
    ];

    ignores = [
      "**/.claude/settings.local.json"
    ];

    settings = {
      # User configuration
      user = {
        name = "${gitUsername}";
        # Email is configured in ~/.config/git/secrets (from encrypted secret)
      };

      # FOSS-friendly settings
      push.default = "simple"; # Match modern push behavior
      credential.helper = "cache --timeout=7200";
      init.defaultBranch = "main"; # Set default new branches to 'main'
      log.decorate = "full"; # Show branch/tag info in git log
      log.date = "iso"; # ISO 8601 date format
      # Conflict resolution style for readable diffs
      merge.conflictStyle = "diff3";

      # URL remapping for SSH access
      url."git@github.com:".insteadOf = "https://github.com/";

      # Git maintenance - automatic repository optimization
      maintenance = {
        auto = true;
        strategy = "incremental";
      };

      # Rerere - Reuse recorded resolution of conflicted merges
      rerere = {
        enabled = true;
        autoupdate = true;
      };

      # Better branch display
      branch.sort = "-committerdate";
      column.ui = "auto";

      # Fetch improvements
      fetch = {
        prune = true; # Remove deleted remote branches
        pruneTags = true; # Remove deleted remote tags
        parallel = 0; # Auto-detect parallelism
      };

      # Pull settings
      pull.rebase = true; # Rebase by default instead of merge

      # Rebase improvements
      rebase = {
        autoStash = true; # Automatically stash before rebase
        autoSquash = true; # Auto-squash fixup commits
      };

      # Delta configuration for better diffs
      core.pager = "delta";
      interactive.diffFilter = "delta --color-only";
      delta = {
        navigate = true;
        light = false;
        side-by-side = true;
        line-numbers = true;
        syntax-theme = "Dracula";
        features = "decorations";
      };
      delta.decorations = {
        commit-decoration-style = "bold yellow box ul";
        file-style = "bold yellow ul";
        file-decoration-style = "none";
      };
      diff.colorMoved = "default";

      # Git subcommand aliases
      alias = {
        # Undo operations
        undo = "reset --soft HEAD~1";
        unstage = "reset HEAD --";
        discard = "checkout --";
        nuke = "!git reset --hard HEAD && git clean -fd";

        # Quick commits
        fixup = "commit --fixup";
        wip = "commit -am 'WIP'";
        amend = "commit --amend --no-edit";

        # Info & inspection
        last = "log -1 HEAD --stat";
        graph = "log --graph --oneline --decorate --all";
        who = "shortlog -sn --no-merges";
        today = "log --since='midnight' --oneline --author";
        recent = "branch --sort=-committerdate --format='%(refname:short) (%(committerdate:relative))'";

        # Branch management
        branches = "branch -vv";
        gone = "!git fetch -p && git branch -vv | grep ': gone]' | awk '{print $1}'";
        cleanup = "!git gone | xargs -r git branch -D";
        default = "!git symbolic-ref refs/remotes/origin/HEAD | sed 's@^refs/remotes/origin/@@'";
        sync = "!git fetch --all --prune && git rebase origin/$(git default)";

        # Diff helpers
        staged = "diff --staged";
        conflicts = "diff --name-only --diff-filter=U";
        changed = "diff --name-only";

        # Stash helpers
        stash-all = "stash push --include-untracked";
        snapshot = "!git stash push -m \"snapshot: $(date)\" && git stash apply";

        # Root of repo
        root = "rev-parse --show-toplevel";
        exec = "!exec ";

        # Meta
        aliases = "!git config --list | grep ^alias | sed 's/alias\\.//'";
      };
    };
  };
}
